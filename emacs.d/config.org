* Initialize
Define the very basics of things needed to run this configuration and
emacs

** Start Emacs Server
Use emacs server so that loading any subsequent emacs instances will
boot immediately. In practice I use that for the occasion I use git on
the command line

#+BEGIN_SRC emacs-lisp
  (server-start)
#+END_SRC

** Local configs
Rather than having custom change configurations in my emacs file
point it to a local file that is not saved in version control.

#+BEGIN_SRC emacs-lisp
  (setq custom-file "~/.emacs.local")
  (load custom-file :noerror)
#+END_SRC

** Package Management(use-package)
use-package will manage our all of our packages. It requires no
external dependencies and takes advantage of emacs' package library

Manage packages and sources
#+BEGIN_SRC emacs-lisp
  (add-to-list 'package-archives
               '("melpa" . "https://melpa.org/packages/") t)
#+END_SRC

bootstrap use-package, so I can use it to manage everything else
#+BEGIN_SRC emacs-lisp
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))

  (setq use-package-verbose t)
  (setq use-package-always-ensure t)
#+END_SRC

Finally require use-package
#+BEGIN_SRC emacs-lisp
  (eval-when-compile (require 'use-package))
#+END_SRC

** Diminish
Diminish is used to simplify the modebar
#+BEGIN_SRC emacs-lisp
(use-package diminish)
#+END_SRC

* Visuals
** Interface
Build interface to look how I want it to look

Remove all that excess crap I don't need, remove the menu bar the
scroll bar and the toolbar

#+BEGIN_SRC emacs-lisp
  (menu-bar-mode -1)
  (scroll-bar-mode -1)
  (tool-bar-mode -1)
#+END_SRC

Set font that is bit easier to read and nice and big

#+BEGIN_SRC emacs-lisp
  (set-default-font "-*-DejaVu Sans Mono-normal-normal-normal-*-22-*-*-*-m-0-iso10646-1")
#+END_SRC

Give more descriptive names to buffers that have the same name. In
this case it uses the folder it's in

#+BEGIN_SRC emacs-lisp
  (require 'uniquify)
  (setq uniquify-buffer-name-style 'post-forward-angle-brackets)
#+END_SRC

Why write out yes or no every single time when y and n will suffice

#+BEGIN_SRC emacs-lisp
  (fset 'yes-or-no-p 'y-or-n-p)
#+END_SRC

Always require a new line

#+BEGIN_SRC emacs-lisp
  (setq require-final-newline t)
#+END_SRC

I like my lines highlighted and numbered

#+BEGIN_SRC emacs-lisp
  (global-hl-line-mode t)
  (global-linum-mode t)
#+END_SRC

Visual cue for when lines are too long

#+BEGIN_SRC emacs-lisp
  (use-package whitespace
    :diminish whitespace-mode
    :init
    (setq whitespace-line-column 80)
    (setq whitespace-style '(face lines-tail))
    (add-hook 'prog-mode-hook 'whitespace-mode))
#+END_SRC

Never use tabs and by default lets use two spaces. We can change it
depending on the language

#+BEGIN_SRC emacs-lisp
  (setq-default indent-tabs-mode nil)
  (setq-default standard-indent 2)
#+END_SRC

When searching make to highlight things I search for

#+BEGIN_SRC emacs-lisp
  (setq query-replace-highlight t)
#+END_SRC

Backups are more trouble than they are worth. That's why God (Linus
Torvald) created git

#+BEGIN_SRC emacs-lisp
  (setq make-backup-files nil)
  (setq auto-save-list-file-name  nil)
  (setq auto-save-default nil)
#+END_SRC

Automatically revert buffers to new content if unchanged

#+BEGIN_SRC emacs-lisp
  (put 'erase-buffer 'disabled nil)
  (global-auto-revert-mode t)
  (diminish 'auto-revert-mode)
#+END_SRC

Remove the whole start up screen and show a visible bell

#+BEGIN_SRC emacs-lisp
  (setq inhibit-startup-message t)
#+END_SRC

Show visible bell, good for when the sound is muted

#+BEGIN_SRC emacs-lisp
  (setq visible-bell t)
#+END_SRC

Make dired more verbose

#+BEGIN_SRC emacs-lisp
  (setq-default dired-listing-switches "-alhv")
#+END_SRC

When splitting buffers move to that new buffer immediately

#+BEGIN_SRC emacs-lisp
  (defun split-window-below-and-move()
    (interactive)
    (split-window-below)
    (other-window 1))

  ;; auto move cursor after split
  (defun split-window-right-and-move()
    (interactive)
    (split-window-right)
    (other-window 1))

  (global-set-key "\C-x2" 'split-window-below-and-move)

  (global-set-key "\C-x3" 'split-window-right-and-move)
#+END_SRC

Ask before closing Emacs

#+BEGIN_SRC emacs-lisp
(defun ask-before-closing ()
  "Ask whether or not to close, and then close if y was pressed"
  (interactive)
  (if (y-or-n-p (format "Exit Emacs? "))
      (if (< emacs-major-version 22)
          (save-buffers-kill-terminal)
        (save-buffers-kill-emacs))
    (message "Canceled exit")))

(when window-system
  (global-set-key (kbd "C-x C-c") 'ask-before-closing))
#+END_SRC

** Theme
Use a color theme

#+BEGIN_SRC emacs-lisp
  (use-package color-theme
    :bind
    ("C-|" . tabfugnic/cycle-theme)
    :init
    (use-package solarized-theme)
    (defun circular (items) (setf (cdr (last items)) items) items)
    (setq tabfugnic/theme-list (circular '(solarized-light solarized-dark)))
    (defun tabfugnic/cycle-theme()
      (interactive)
      (load-theme (pop tabfugnic/theme-list) t))
    (tabfugnic/cycle-theme))
#+END_SRC

** Golden ratio
#+BEGIN_SRC emacs-lisp
  (use-package golden-ratio
    :diminish golden-ratio-mode
    :init
    (setq golden-ratio-auto-scale nil)
    (setq golden-ratio-extra-commands
          '(evil-window-left
            evil-window-right
            evil-window-up
            evil-window-down
            select-window-1
            select-window-2
            select-window-3
            select-window-4
            select-window-5))
    :config
    (golden-ratio-mode 1))
#+END_SRC

** Wrap region
#+BEGIN_SRC emacs-lisp
(use-package wrap-region
  :config
  (wrap-region-mode t))
#+END_SRC

** Date
Use ISO date standard

#+BEGIN_SRC emacs-lisp
  (setq calendar-date-style 'iso)
#+END_SRC

* Env
#+BEGIN_SRC emacs-lisp
  (let ((go-path (shell-command-to-string ". ~/.bashrc; echo -n $GOPATH")))
    (setenv "GOPATH" go-path))
#+END_SRC
* Edit Text
** Evil Mode

Use evil mode for most editing

#+BEGIN_SRC emacs-lisp
  (use-package evil
    :diminish undo-tree-mode
    :init
    (setq evil-toggle-key "C-`")
    (use-package linum-relative)
    (use-package evil-surround
      :config
      (global-evil-surround-mode 1))
    :config
    (evil-mode 1))
#+END_SRC

** Editing
No need to have trailing whitespace

#+BEGIN_SRC emacs-lisp
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+END_SRC

Use CUA for block editing

#+BEGIN_SRC emacs-lisp
  (setq cua-enable-cua-keys nil)
  (cua-mode t)
#+END_SRC

Use Hippie Expand to expand things

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "M-SPC") 'hippie-expand)
#+END_SRC

Move by subwords ie treat underscore as space

#+BEGIN_SRC emacs-lisp
  (global-subword-mode t)
  (diminish 'subword-mode)
#+END_SRC

Set default spacing for all documents
#+BEGIN_SRC emacs-lisp
  (setq default-tab-width 2)
  (setq sh-basic-offset 2)
  (setq sh-indentation 2)
#+END_SRC

Break on commas
#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-,") 'tabfugnic/break-on-comma)
  (global-set-key (kbd "C-;") 'tabfugnic/toggle-single-or-multiline-list)

  (defun tabfugnic/break-on-comma()
    (interactive)
    (while (not (looking-at ","))
      (forward-char))
    (forward-char)
    (if (not (looking-at "^J"))
        (newline-and-indent)))

  (defun tabfugnic/toggle-single-or-multiline-list()
    (interactive)
    (if (not (tabfugnic/singleline-p))
        (tabfugnic/singleline-list)
      (tabfugnic/multiline-list)))

  (defun tabfugnic/multiline-list()
    (interactive)
    (let ((beg (point)))
      (forward-char)
      (newline-and-indent)
      (end-of-line 0)
      (backward-char)
      (forward-list)
      (backward-char)
      (if (not (looking-at ","))
          (insert ","))
      (while (<= beg (point))
        (backward-char)
        (when (and (looking-at ",") (tabfugnic/in-parent-list-p beg))
          (forward-char)
          (newline-and-indent)
          (previous-line)
          (end-of-line)
          (backward-char))
        )
      (forward-char)))

  (defun tabfugnic/singleline-list()
    (interactive)
    (let ((beg (line-number-at-pos)))
      (forward-list)
      (while (< beg (line-number-at-pos))
        (join-line))
      (backward-char)
      (forward-list)
      (backward-char 2)
      (if (looking-at ",")
          (delete-forward-char 1))
      (forward-char)
      (backward-list)))

  (defun tabfugnic/singleline-p()
    (let ((beg-line (line-number-at-pos)) (start (point)))
      (forward-list)
      (let ((end-line (line-number-at-pos)))
        (goto-char start)
        (eq beg-line end-line))))

  (defun tabfugnic/in-parent-list-p(parent-beg)
    (let ((start (point)))
      (backward-up-list)
      (when (eq parent-beg (point))
        (goto-char start)
        t)))

#+END_SRC

** Multiple Cursor
#+BEGIN_SRC emacs-lisp
  (use-package multiple-cursors
    :bind
    ("C-S-c C-S-c" . mc/edit-lines)
    ("C->" . mc/mark-next-like-this)
    ("C-<" . mc/mark-previous-like-this)
    ("C-c C-<" . mc/mark-all-like-this))
#+END_SRC

** Electric pair

Use electric pair to automatically match surrounding characters

#+BEGIN_SRC emacs-lisp
  (electric-pair-mode 1)
  (show-paren-mode 1)
#+END_SRC

** Move shit
#+BEGIN_SRC emacs-lisp
(use-package move-text
  :config
  (move-text-default-bindings))
#+END_SRC
* Support
** Company
#+BEGIN_SRC emacs-lisp
  (use-package company
    :diminish company-mode
    :init
    (add-hook 'after-init-hook 'global-company-mode))
#+END_SRC

** Yasnippet
#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :diminish yas-minor-mode
    :config
    (yas-global-mode 1))
#+END_SRC

** flycheck
#+BEGIN_SRC emacs-lisp
(use-package flycheck
  :init
  (add-hook 'after-init-hook #'global-flycheck-mode))
#+END_SRC

* Navigation
** Helm

Helm for most things involving input

#+BEGIN_SRC emacs-lisp
  (use-package helm
    :diminish helm-mode
    :bind
    ("M-x" . helm-M-x)
    ("C-S-s" . helm-do-ag-project-root)
    ("C-x C-f" . helm-find-files)
    :init
    (use-package helm-ag
      :init
      (setq helm-M-x-fuzzy-match t))
    (progn
      (require 'helm-config)
      (helm-mode))
    (use-package helm-tramp
      :config
      (add-hook 'helm-tramp-pre-command-hook
                '(lambda ()
                   (projectile-mode 0)))
      (add-hook 'helm-tramp-quit-hook
                '(lambda ()
                   (projectile-mode 1)))))
#+END_SRC

** Dired

Use a better dired, now with extras

#+BEGIN_SRC emacs-lisp
(require 'dired-x)
#+END_SRC

** Directory
#+BEGIN_SRC emacs-lisp
  (defun tabfugnic/next-file-in-directory(&optional number)
    (interactive)
    (or number (setq number 1))
    (let
        ((filename (file-name-nondirectory (buffer-file-name)))
         (files (remove-if
                 'file-directory-p
                 (directory-files
                  (file-name-directory (buffer-file-name))
                  nil
                  directory-files-no-dot-files-regexp))))
      (find-file
       (nth (mod (+ (position filename files :test 'equal) number)
         (length files))
        files))))

  (defun tabfugnic/previous-file-in-directory()
    (interactive)
    (tabfugnic/next-file-in-directory -1))


  (global-set-key (kbd "C-{") 'tabfugnic/previous-file-in-directory)
  (global-set-key (kbd "C-}") 'tabfugnic/next-file-in-directory)
#+END_SRC

** Projectile
#+BEGIN_SRC emacs-lisp
    (use-package projectile
      :diminish projectile-mode
      :init
      (use-package helm-projectile)
      :config
      (define-key projectile-mode-map (kbd "C-c p") 'projectile-command-map)
      (projectile-global-mode))
#+END_SRC

** Global and GGTags
  #+BEGIN_SRC emacs-lisp
(use-package ggtags
  :config
  (add-hook 'ruby-mode-hook ( lambda() ( ggtags-mode 1 ))))
#+END_SRC

** Important Files
  #+BEGIN_SRC emacs-lisp
    (defun tabfugnic/open-inbox ()
    (interactive)
     (find-file (expand-file-name "~/cloud/org/inbox.org")))

    (global-set-key (kbd "C-c i") 'tabfugnic/open-inbox)
#+END_SRC

* Language/Programming
** Android

Use android major mode

#+BEGIN_SRC emacs-lisp
  (use-package android-mode
    :config
    (custom-set-variables '(android-mode-sdk-dir "~/opt/android")))
#+END_SRC

** C/C++

#+BEGIN_SRC emacs-lisp
  (c-set-offset 'arglist-intro '+)
#+END_SRC

** Cucumber
#+BEGIN_SRC emacs-lisp
(use-package feature-mode
  :mode "\\.feature$")
#+END_SRC

** Emacs Lisp

Auto compile elisp files on load/save.

#+BEGIN_SRC emacs-lisp
  (use-package auto-compile
    :config
    (auto-compile-on-load-mode)
    (auto-compile-on-save-mode))
  (setq load-prefer-newer t)
#+END_SRC

** Go
#+BEGIN_SRC emacs-lisp
  (use-package go-mode
    :init
    (use-package gotest
      :bind
      (:map go-mode-map
            ("C-c , v" . go-test-current-test)
            ("C-c , a" . go-test-current-project)
            ("C-c , b" . go-test-current-benchmark)
            ("C-c , x" . go-run))))
#+END_SRC
** Haskell
#+BEGIN_SRC emacs-lisp
(use-package haskell-mode
  :init
  (add-hook 'haskell-mode-hook 'turn-on-haskell-indent)
  (add-hook 'haskell-mode-hook 'turn-on-haskell-decl-scan)
  :bind
  (:map haskell-mode-map
        ("C-," . haskell-move-nested-left)
        ("C-." . haskell-move-nested-right)
        ("C-c C-c" . haskell-compile)))
#+END_SRC

** HTML/CSS
#+BEGIN_SRC emacs-lisp
(use-package emmet-mode
  :init
  (add-hook 'emmet-mode-hook (lambda () (setq emmet-indentation 2))))
#+END_SRC

** Java

Add imports to java file quickly and easily
#+BEGIN_SRC emacs-lisp
  (use-package java-imports)
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package eclim
  :config
  (global-eclim-mode))
#+END_SRC

** Javascript/Coffee
#+BEGIN_SRC emacs-lisp
(use-package coffee-mode
  :mode ("\\.coffee$" "Cakefile")
  :init
  (setq coffee-tab-width 2))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package js2-mode
  :init
  (setq auto-mode-alist (cons '("\\.js$" . javascript-mode) auto-mode-alist))
  (setq js-indent-level 2))
#+END_SRC

#+BEGIN_SRC emacs-lisp
;; JSX mode
(use-package rjsx-mode)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package vue-mode
    :init
    (setq mmm-submode-decoration-level 0))
#+END_SRC

** JSON
Basic json parsing

#+BEGIN_SRC emacs-lisp
  (use-package json-mode)
#+END_SRC

** LSP
Setup the language server protocol

#+BEGIN_SRC emacs-lisp
    (use-package lsp-mode
      :hook ((rust-mode . lsp)
             (ruby-mode . lsp)
             (go-mode . lsp))
      :commands lsp)

    (use-package lsp-ui
      :commands lsp-ui-mode
      :config
      (setq lsp-prefer-flymake nil))
    (use-package company-lsp :commands company-lsp)
    (use-package helm-lsp :commands helm-lsp-workspace-symbol)
    (use-package dap-mode)
#+END_SRC

** Lua
#+BEGIN_SRC emacs-lisp
  (use-package lua-mode)
#+END_SRC
** Markdown
#+BEGIN_SRC emacs-lisp
(use-package markdown-mode
  :init
  (add-to-list 'auto-mode-alist '("\\.text\\'" . markdown-mode))
  (add-to-list 'auto-mode-alist '("\\.markdown\\'" . markdown-mode))
  (add-to-list 'auto-mode-alist '("\\.md\\'" . markdown-mode)))
#+END_SRC
** PHP
#+BEGIN_SRC emacs-lisp
(use-package php-mode
  :init
  (setq auto-mode-alist (cons '("\\.php$" . php-mode) auto-mode-alist))
  (setq auto-mode-alist (cons '("\\.module$" . php-mode) auto-mode-alist))
  (setq auto-mode-alist (cons '("\\.install$" . php-mode) auto-mode-alist))
  (setq auto-mode-alist (cons '("\\.inc$" . php-mode) auto-mode-alist))

  (setq interpreter-mode-alist (cons '("php" . php-mode) interpreter-mode-alist))
  (setq auto-mode-alist (cons '("\\.phtml$" . php-mode) auto-mode-alist))
  (setq interpreter-mode-alist (cons '("phtml" . php-mode) interpreter-mode-alist)))
#+END_SRC

** Ruby/Rails
#+BEGIN_SRC emacs-lisp
(use-package chruby
  :config (chruby "2.5.0"))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package ruby-mode
    :init
    (use-package inf-ruby
      :init (add-hook 'after-init-hook 'inf-ruby-switch-setup))
    (use-package ruby-end
      :diminish ruby-end-mode)
    (use-package ruby-hash-syntax
      :bind ("C-c r h" . ruby-toggle-hash-syntax))
    (setq auto-mode-alist (cons '("\\.rb$" . ruby-mode) auto-mode-alist))
    (setq auto-mode-alist (cons '("\\.rake$" . ruby-mode) auto-mode-alist))
    (setq auto-mode-alist (cons '("Gemfile" . ruby-mode) auto-mode-alist))
    (setq auto-mode-alist (cons '("Guardfile" . ruby-mode) auto-mode-alist))
    (setq auto-mode-alist (cons '("Rakefile" . ruby-mode) auto-mode-alist))
    (setq interpreter-mode-alist (append '(("ruby" . ruby-mode)) interpreter-mode-alist)))
#+END_SRC

Use web dev for ERB and html. Makes life so much easier than Multi Major Mode

#+BEGIN_SRC emacs-lisp
(use-package web-mode
  :init
  (add-to-list 'auto-mode-alist '("\\.html\\.erb$" . web-mode))
  (add-to-list 'auto-mode-alist '("\\.liquid$" . web-mode)))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package rspec-mode
    :bind (:map rspec-mode ("C-c , u" . tabfugnic/rspec-set-test-env))
    :init
    (add-hook 'haml-mode-hook 'rspec-mode)
    (add-hook 'html-mode-hook 'rspec-mode)
    (add-hook 'slim-mode-hook 'rspec-mode)
    (add-hook 'web-mode-hook 'rspec-mode)
    (add-hook 'coffee-mode-hook 'rspec-mode)
    (add-hook 'ruby-mode-hook 'chruby-use-corresponding)
    (setq rspec-command-options "--format progress")
    :config
    (defun tabfugnic/rspec-set-test-env()
        (interactive)
        (let ((root (directory-file-name (rspec-project-root))))
          (message "setting testing environment database")
          (shell-command (format "%s/bin/rails db:environment:set RAILS_ENV=test" root)))))
#+END_SRC

#+BEGIN_SRC emacs-lisp
(use-package lsp-ruby
  :ensure t
  :config
  (require 'company-lsp)
  (push 'company-lsp company-backends)
  :hook
  ((ruby-mode . lsp)
  (ruby-mode . (lambda ()
                  (setq company-idle-delay .2
                        company-tooltip-limit 20)
                  (set(make-local-variable 'company-backends) '(company-lsp))))))
#+END_SRC

** Rust
#+BEGIN_SRC emacs-lisp
  (use-package rust-mode
    :init
    (setq rust-format-on-save 1))
  (use-package cargo
    :ensure t
    :init
    (setenv "PATH" (concat (getenv "PATH") ":~/.cargo/bin"))
    (setq exec-path (append exec-path '("~/.cargo/bin"))))
  (use-package flycheck-rust
    :init
    (add-hook 'flycheck-mode-hook #'flycheck-rust-setup))
#+END_SRC
** SCSS Mode
#+BEGIN_SRC emacs-lisp
(use-package scss-mode
  :init
  (setq auto-mode-alist (cons '("\\.scss$" . scss-mode) auto-mode-alist))
  (setq auto-mode-alist (cons '("\\.scss.erb$" . scss-mode) auto-mode-alist))
  (setq interpreter-mode-alist (cons '("scss" . scss-mode) interpreter-mode-alist))
  (setq interpreter-mode-alist (cons '("scss.erb" . scss-mode) interpreter-mode-alist))
  (setq scss-compile-at-save nil)
  (setq css-indent-offset 2))
#+END_SRC

** Slim/Haml
Definitely need Slim and HAML mode

#+BEGIN_SRC emacs-lisp
  (use-package slim-mode)
  (use-package haml-mode)
#+END_SRC

** Yaml
#+BEGIN_SRC emacs-lisp
(use-package yaml-mode
  :init
  (setq auto-mode-alist (cons '("\\.yml$" . yaml-mode) auto-mode-alist))
  (setq interpreter-mode-alist (cons '("yaml" . yaml-mode) interpreter-mode-alist)))
#+END_SRC

* Applications
** RSS
Newsticker for RSS feeds

#+BEGIN_SRC emacs-lisp
  (use-package elfeed
    :bind ("C-x w" . elfeed)
    :init
    (use-package elfeed-org
      :init (setq rmh-elfeed-org-files (list "~/cloud/org/rss.org"))
      :config (elfeed-org))
    (evil-set-initial-state 'elfeed-search-mode 'emacs)
    (evil-set-initial-state 'elfeed-show-mode 'emacs))
#+END_SRC

** Blog

Setup blog
#+BEGIN_SRC emacs-lisp
  (setq tabfugnic/blog-dir "~/blog")
  (setq tabfugnic/blog-posts-dir (expand-file-name "_posts" tabfugnic/blog-dir))

  (defun tabfugnic/blog-new-entry(title)
    (interactive "MTitle: ")
    (let ((slug (tabfugnic/sluggify title)))
      (find-file (expand-file-name
                       (concat (format-time-string "%F") "-" slug ".md")
                       tabfugnic/blog-posts-dir))
      (insert "---\n")
      (insert "layout: post\n")
      (insert (format "title: %s\n" title))
      (insert (format "date: %s\n" (format-time-string "%F %R")))
      (insert "tags: \n")
      (insert "---\n")))

  (defun tabfugnic/sluggify(string)
    (replace-regexp-in-string
     "[^a-z0-9-]" "" (replace-regexp-in-string
                      "\\\s" "-" (downcase string))))
#+END_SRC

** Email(mu4e)
Use mu4e for all email. This takes advantage of offlineimap and msmtp

#+BEGIN_SRC emacs-lisp
  (use-package mu4e
    :load-path "/usr/local/share/emacs/site-lisp/mu4e"
    :ensure f
    :bind ("C-x m" . mu4e)
    :init
    (add-hook 'mu4e-view-mode-hook 'visual-line-mode)
    (add-hook 'mu4e-compose-mode-hook 'mml-secure-message-sign)
    (add-hook 'mu4e-compose-mode-hook
              (defun my-setup-epa-hook ()
                (epa-mail-mode)))
    (add-hook 'mu4e-view-mode-hook
              (defun my-view-mode-hook ()
                (epa-mail-mode)))
    (use-package mu4e-alert
      :init
      (setq mu4e-maildir-shortcuts
            '( ("/INBOX"               . ?i)
               ("/sent"                . ?s)
               ("/archive"             . ?a)))

      (mu4e-alert-set-default-style 'libnotify)
      (add-hook 'after-init-hook #'mu4e-alert-enable-notifications)
      (add-hook 'after-init-hook #'mu4e-alert-enable-mode-line-display))


    :config
    (require 'org-mu4e)
    (setq mu4e-contexts
          `(,(make-mu4e-context
              :name "personal"
              :match-func (lambda (msg)
                            (when msg
                              (mu4e-message-contact-field-matches
                               msg
                               :to "me@ericj.co")))
              :enter-func '()
              :leave-func (lambda () (mu4e-clear-caches))
              :vars '((mu4e-maildir . "~/mail/personal")
                      (mu4e-mu-home . "~/.mu/personal")
                      (user-mail-address . "me@ericj.co")
                      (mu4e-compose-signature . (concat
                                                 "Eric J. Collins\n"
                                                 "Software Developer\n"
                                                 "thoughtbot\n")
                                              )))
            ,(make-mu4e-context
              :name "thoughtbot"
              :match-func (lambda (msg)
                            (when msg
                              (mu4e-message-contact-field-matches
                               msg
                               :to "eric@thoughtbot.com")))
              :enter-func '()
              :leave-func (lambda () (mu4e-clear-caches))
              :vars '((mu4e-maildir . "~/mail/thoughtbot")
                      (mu4e-mu-home . "~/.mu/thoughtbot")
                      (user-mail-address . "eric@thoughtbot.com")
                      (mu4e-compose-signature . (concat
                                                 "Eric J. Collins\n"
                                                 "Developer and Development Director\n"
                                                 "thoughtbot\n")
                                              )))))

    (setq mu4e-drafts-folder "/drafts")
    (setq mu4e-sent-folder   "/sent")
    (setq mu4e-trash-folder  "/trash")
    (setq mu4e-refile-folder "/archive")
    (setq mu4e-action-tags-header "X-Keywords")
    (setq mu4e-attachment-dir  "~/Downloads")
    (setq mu4e-html2text-command 'mu4e-shr2text)
    (setq mu4e-compose-dont-reply-to-self t)
    (setq user-full-name  "Eric J Collins")
    (setq mu4e-update-interval 180)
    (setq mu4e-user-mail-address-list '("eric@thoughtbot.com" "me@ericj.co"))
    (setq mu4e-change-filenames-when-moving t)

    (setq message-send-mail-function 'message-send-mail-with-sendmail)
    (setq sendmail-program "/usr/local/bin/msmtp-enqueue.sh")
    (setq message-sendmail-extra-arguments '("--read-envelope-from"))
    (setq message-sendmail-f-is-evil 't)

    (setq mu4e-alert-interesting-mail-query
          (concat
           "flag:unread"
           " AND NOT flag:trashed"
           " AND maildir:"
           "\"/INBOX\""))

    (setq org-mu4e-link-query-in-headers-mode nil)

    (add-to-list 'mu4e-headers-custom-markers
                 '("Stale messages"
                   (lambda (msg &optional n)
                     (let ((email (cdar (mu4e-message-field msg :from))))
                       (or (string-match "\\@nytimes.com" email)
                           (string-match "\\@trello.com" email)
                           (string-match "\\@github.com" email)
                           (string-match "\\@notifications.heroku.com" email))))))

    (add-to-list 'mu4e-view-actions
                 '("xViewXWidget" . mu4e-action-view-with-xwidget) t)

    (defun tabfugnic/mu4e-headers-mark-stale-for-delete()
      (interactive)
      (mu4e-headers-for-each
       (lambda (msg)
         (let ((tags (mu4e-message-field msg :tags))
               (date (mu4e-message-field msg :date))
               (one-day-ago (subtract-time (current-time) 86400)))
           (when (and (member "temporary" tags) (time-less-p date one-day-ago))
             (mu4e-mark-at-point 'trash msg)))))))
#+END_SRC

** ERC
#+BEGIN_SRC emacs-lisp
(use-package erc
  :bind ("C-c e r" . tabfugnic/reset-erc-track-mode)
  :init

  (use-package erc-image
    :config
    (add-to-list 'erc-modules 'image))

  (setq erc-prompt-for-nickserv-password nil)
  (setq erc-fill-function 'erc-fill-static)
  (setq erc-fill-static-center 22)
  (setq erc-track-exclude-types '("JOIN" "NICK" "PART" "MODE"))
  (setq erc-hide-list '("JOIN" "PART" "QUIT" "MODE"))

  (setq erc-keywords '("\\NYC\\b"
                       "\\nyc\\b"
                       "\\pr\\b"
                       "\\PR\\b"
                       "\\:statue_of_liberty:\\b"
                       "\\corgi\\b"))

  (setq ercn-notify-rules
        '((current-nick . all)
          (keyword . all)))

  (add-hook 'ercn-notify 'tabfugnic/do-notify)

  :config
  (erc-update-modules)

  (require 'erc-join)
  (setq erc-autojoin-channels-alist
        '(("freenode.net" "#thoughtbot" "#emacs" "#emacsnyc")))
  (erc-autojoin-enable))

(defun tabfugnic/erc-start-or-switch ()
  "Connect to ERC, or switch to last active buffer"
  (interactive)
  (cond
    ((get-buffer "irc.freenode.net:6667")
     (erc-track-switch-buffer 1))
    (t (erc :server "irc.freenode.net" :port 6667 :nick "tabfugnic"))))

(defun tabfugnic/reset-erc-track-mode ()
  (interactive)
  (setq erc-modified-channels-alist nil)
  (erc-modified-channels-display))

(defun tabfugnic/do-notify (nickname message)
  (with-temp-buffer
    (shell-command (format "notify-send '%s: %s' -t 5000" nickname message) t)))
#+END_SRC
* Git(Magit)
#+BEGIN_SRC emacs-lisp
  (use-package magit
    :bind ("C-x g" . magit-status)
    :config
    (magit-define-popup-action 'magit-branch-popup
      ?K "Delete local and remote" 'tabfugnic/magit-delete-branch)

    (defun tabfugnic/magit-delete-branch (branch)
      (interactive
       (magit-read-local-branch "Delete branch" (magit-get-previous-branch)))
      (magit-run-git "delete-branch" "" branch)))
#+END_SRC

* Org mode
#+BEGIN_SRC emacs-lisp
  (use-package org
    :bind (("C-c l" . org-store-link)
           ("C-c c" . org-capture)
           ("C-c a" . org-agenda)
           ("C-c b" . org-iswitchb)
           :map org-agenda-mode-map
           ("C-c f" . tabfugnic/org-agenda-complete-and-archive)
           :map org-mode-map
           ("C-c f" . tabfugnic/org-agenda-complete-and-archive))
    :init
    (use-package org-journal
      :init
      (setq org-journal-dir "~/cloud/journal")
      (setq org-journal-date-format "%A %Y/%m/%d")
      (add-hook 'org-journal-mode-hook 'auto-fill-mode))
    (use-package org-caldav
      :init
      (setq org-caldav-url "https://cloud.ericj.co/remote.php/dav/addressbooks/users/tabfugnic")
      (setq org-caldav-calendar-id "Contacts")
      (setq org-caldev-inbox "~/contacts.org"))

    (org-babel-do-load-languages
     'org-babel-load-languages
     '((shell . t)))
    (require 'org-agenda)
    (setq org-directory "~/cloud/org/")

    (defun tabfugnic/org-file (file)
      (concat org-directory file))

    (setq org-agenda-files (list (tabfugnic/org-file "todos.org")))

    (setq org-capture-templates
          `(("i" "Inbox"
             entry
             (file ,(tabfugnic/org-file "inbox.org"))
             "* %?\n:PROPERTIES:\n:CREATED: %U\n:END:\n")
            ("m" "Mail Inbox"
             entry
             (file ,(tabfugnic/org-file "inbox.org"))
             "* %?\n%a\n:PROPERTIES:\n:CREATED: %U\n:END:\n")
            ("c" "Copy Inbox"
             entry
             (file ,(tabfugnic/org-file "inbox.org"))
             "* %?\n:PROPERTIES:\n:CREATED: %U\n:END:\n%x\n")
            ("t" "TODO"
             entry
             (file ,(tabfugnic/org-file "todos.org"))
             "* TODO %?\n")
            ("h" "House"
             entry
             (file ,(tabfugnic/org-file "house.org"))
             "* %?\n[[%x][%^{link}]]\n")
            ("v" "Travel"
             plain
             (file ,(tabfugnic/org-file "travel.org"))
             "* %a\n %^G\n")
            ("m" "Movies"
             entry
             (file ,(tabfugnic/org-file "movies.org"))
             "* TODO %?\n %u\n")
            ("r" "RSS Feeds"
             entry
             (file+headline ,(tabfugnic/org-file "rss.org") "Unsorted Feeds")
             "*** [[%x][%?]]")))

    (defun tabfugnic/org-agenda-complete-and-archive ()
      (interactive)
      (org-agenda-todo 'done)
      (org-agenda-archive-default))

    (defun tabfugnic/org-complete-and-archive ()
      (interactive)
      (org-todo 'done)
      (org-archive-default))

    ;; Taken from
    ;; https://www.reddit.com/r/emacs/comments/6lzyg2/heres_how_to_do_emacsclient_global_orgcapture/
    ;; Modified slightly
    (defadvice org-capture-finalize
        (after delete-capture-frame activate)
      "Advise capture-finalize to close the frame"
      (if (equal "capture" (frame-parameter nil 'name))
          (delete-frame)))

    (defadvice org-capture-destroy
        (after delete-capture-frame activate)
      "Advise capture-destroy to close the frame"
      (if (equal "capture" (frame-parameter nil 'name))
          (delete-frame)))

    (defun make-capture-frame (&optional capture-key)
      "Create a new frame and run org-capture."
      (interactive)
      (or capture-key (setq capture-key "i"))
      (make-frame '((name . "capture")))
      (select-frame-by-name "capture")
      (delete-other-windows)
      (cl-letf
          (((symbol-function 'switch-to-buffer-other-window)
            #'switch-to-buffer))
        (org-capture nil capture-key))))
#+END_SRC

* Utils

  #+BEGIN_SRC emacs-lisp
    ;; (setq tabfugnic/previous-commands '())
    (set-face-attribute 'comint-highlight-prompt nil
                        :inherit nil)

    (defun tabfugnic/repeat-shell-commandline-in-project()
      (interactive)
      (setq tabfugnic/previous-command)
      ;; (setq tabfugnic/previous-commands
      ;;       (cons '((vc-root-dir) . "thing") tabfugnic/previous-commands))
    )
  #+END_SRC


  #+BEGIN_SRC emacs-lisp
(defun gitrep()
  (interactive "*")
  (find-file "~/dev"))
  #+END_SRC

  Sort lines with out case
  #+BEGIN_SRC emacs-lisp
  (defun sort-lines-nocase ()
    (interactive)
    (let ((sort-fold-case t))
      (call-interactively 'sort-lines)))
  #+END_SRC
